pkgname="carbonio-prometheus-alertmanager"
pkgver="0.26.0"
pkgrel="2"
pkgdesc="Alert manager exporter for open-source systems monitoring and alerting toolkit"
maintainer="Zextras <packages@zextras.com>"
url="https://zextras.com"
section="mail"
priority="optional"
arch=('x86_64')
license=('Apache-2.0')
source=(
  "https://github.com/prometheus/alertmanager/releases/download/v${pkgver}/alertmanager-${pkgver}.linux-amd64.tar.gz"
  "alertmanager.yml"
  "email.tmpl"
  "telegram.tmpl"
  "${pkgname}.default"
  "${pkgname}.service"
  "prometheus.sysusers"
  "${pkgname}.tmpfiles"
)
sha256sums=('abd73e2ee6bf67d3888699660abbecba7b076bf1f9459a3a8999d493b149ffa6'
  'cce91b60dd9a5e47e4c9ce535bc770f7a388b716839fedc2611a2f63d74ef26c'
  'ae12a6e0514c4087060e4d5246eedbd7bd7d44c3326b480355b45e6aa823ed6b'
  '34b0850a3afd77765fe4b3cca2fffd95564087fb465a39bd8a4e3a7a98a3c17f'
  'fe18f2e90fd2373867328631b8ec6fba405157e826af31272b6ef97e355cfcb4'
  '588d7e361c9cd31458760ad1f7a5fe7cc08d8be276774854f7c7f68873c9101a'
  '48e22f1cf5592c90a62683f3e551fde213258b980d0858d8a7927da0273fe746'
  'e64c0dbcbf49904a1e2b70bf0b01d3a8503cd28912abf8ef677b2e415649e0b8')

depends=(
  "systemd"
)

conflicts=(
  "prometheus-alertmanager"
)

backup=(
  "etc/carbonio/${pkgname}/alertmanager.yml"
  "etc/carbonio/${pkgname}/templates/email.tmpl"
  "etc/carbonio/${pkgname}/templates/telegram.tmpl"
)

package() {
  cd "${srcdir}"

  install -Dm644 "${pkgname}.tmpfiles" \
    "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
  install -Dm 644 "${pkgname}.service" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -Dm 644 "${pkgname}.default" \
    "${pkgdir}/etc/default/${pkgname}"
  install -Dm 644 "prometheus.sysusers" \
    "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"

  cd "${srcdir}/alertmanager-${pkgver}.linux-amd64"

  install -Dm 755 alertmanager \
    "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 755 amtool \
    "${pkgdir}/usr/bin/amtool"

  install -dm 750 "${pkgdir}/var/lib/alertmanager/data"
  install -dm 750 "${pkgdir}/etc/carbonio/${pkgname}/templates"

  install -Dm 644 "${srcdir}/alertmanager.yml" \
    "${pkgdir}/etc/carbonio/${pkgname}/alertmanager.yml"

  install -Dm 644 "${srcdir}/telegram.tmpl" \
    "${pkgdir}/etc/carbonio/${pkgname}/templates/"
  install -Dm 644 "${srcdir}/email.tmpl" \
    "${pkgdir}/etc/carbonio/${pkgname}/templates/"

}

postinst() {
  if hash initctl 2>/dev/null; then
    initctl reload-configuration 2>/dev/null || true
  fi

  systemd-sysusers >/dev/null || true
  systemd-tmpfiles --create >/dev/null || true

  # While in beta enable service manually
  # if [ -d /run/systemd/system ]; then
  # systemctl daemon-reload >/dev/null 2>&1 || :
  # systemctl enable carbonio-prometheus-alertmanager.service >/dev/null 2>&1 || :
  # systemctl start carbonio-prometheus-alertmanager.service >/dev/null 2>&1 || :
  # fi

  echo "======================================================"
  echo "Carbonio Prometheus Alert Manager installed successfully!"
  echo "You can configure it in prometheus.yml"
  echo "While in beta enable service manually"
  echo "======================================================"
}

prerm() {
  if [ -d /run/systemd/system ]; then
    systemctl --no-reload disable carbonio-prometheus-alertmanager.service >/dev/null 2>&1 || :
    systemctl stop carbonio-prometheus-alertmanager.service >/dev/null 2>&1 || :
  fi
}

postrm() {
  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
  fi
}
