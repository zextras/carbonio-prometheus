pkgname="carbonio-prometheus-openldap-exporter"
pkgver="2.2.4"
pkgrel="3"
pkgdesc="OpenLDAP exporter for open-source systems monitoring and alerting toolkit"
maintainer="Zextras <packages@zextras.com>"
url="https://zextras.com"
section="mail"
priority="optional"
arch=('x86_64')
license=('Apache-2.0')
source=(
  "https://github.com/mlorenzo-stratio/openldap_exporter/archive/refs/tags/v${pkgver}.zip"
  "${pkgname}.default"
  "${pkgname}.service"
  "${pkgname}.yml"
  "${pkgname}-consul.hcl"
  "set-${pkgname}.sh"
  "prometheus.sysusers"
  "${pkgname}.tmpfiles"
)
sha256sums=('4615e0e422109af6bdba6488092b5b651b8a014ef9bdf91f37978234d8484ce3'
  '9269776a1c768234c78dc2791bc576c55984aef2986e9d16255d23cc47c1b20f'
  'c369119e4923f6927aa6ee09506d1ff00799f77e36f10715165f6858c657884a'
  '01ba4719c80b6fe911b091a7c05124b64eeece964e09c058ef8f9805daca546b'
  'bc4875f5944b614b00f25db79fa28e5b059156e62352921b82bba4100a387260'
  '234bb5e0dbd37d24ffa16b4f0c9f0697382bc2dac096e38dd9feefeb8966a1c3'
  '48e22f1cf5592c90a62683f3e551fde213258b980d0858d8a7927da0273fe746'
  '6514371196a55f2096c4c65d281eaf2ace8648f94c432d57ff91776e1949d2ec')

depends=(
  "systemd"
)

conflicts=(
  'openldap-exporter'
)

makedepends=(
  "git"
)

backup=(
  "etc/default/${pkgname}"
  "etc/carbonio/${pkgname}/${pkgname}.yml"
  "etc/zextras/service-discover/${pkgname}.hcl"
)

build() {
  cd "${srcdir}/openldap_exporter-${pkgver}/"
  make build-linux
}

package() {

  cd "${srcdir}/"

  install -Dm644 "${pkgname}.tmpfiles" \
  "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"

  install -Dm 755 "./openldap_exporter-${pkgver}/target/openldap_exporter-linux" \
  "${pkgdir}/usr/bin/${pkgname}"

  install -Dm 644 "set-${pkgname}.sh" \
  "${pkgdir}/etc/zextras/pending-setups.d/set-${pkgname}.sh"

  install -Dm 644 "${pkgname}.service" \
  "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -Dm 644 "${pkgname}.default" \
  "${pkgdir}/etc/default/${pkgname}"
  install -Dm 644 "${pkgname}.yml" \
  "${pkgdir}/etc/carbonio/${pkgname}/${pkgname}.yml"

  install -Dm 644 "prometheus.sysusers" \
  "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  install -Dm 644 "${pkgname}-consul.hcl" \
  "${pkgdir}/etc/zextras/service-discover/${pkgname}.hcl"
}

postinst() {
  if hash initctl 2>/dev/null; then
    initctl reload-configuration 2>/dev/null || true
  fi

  systemd-sysusers >/dev/null || true
  systemd-tmpfiles --create >/dev/null || true

  #/usr/set-carbonio-prometheus-openldap-exporter.sh

  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
    systemctl enable carbonio-prometheus-openldap-exporter.service >/dev/null 2>&1 || :
    systemctl start carbonio-prometheus-openldap-exporter.service >/dev/null 2>&1 || :
  fi

  echo "======================================================"
  echo "Carbonio LDAP Exporter installed successfully!"
  echo "You must run pending-setups to configure it correctly."
  echo "======================================================"
}

prerm() {
  if [ -d /run/systemd/system ]; then
    systemctl --no-reload disable carbonio-prometheus-openldap-exporter.service >/dev/null 2>&1 || :
    systemctl stop carbonio-prometheus-openldap-exporter.service >/dev/null 2>&1 || :
  fi
}

postrm() {
  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
  fi
}
