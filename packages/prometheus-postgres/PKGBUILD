pkgname="carbonio-prometheus-postgres-exporter"
pkgver="0.15.0"
pkgrel="1"
pkgdesc="Postgres exporter for open-source systems monitoring and alerting toolkit"
maintainer="Zextras <packages@zextras.com>"
url="https://zextras.com"
section="mail"
priority="optional"
arch=('x86_64')
license=('Apache-2.0')
source=(
  "https://github.com/prometheus-community/postgres_exporter/releases/download/v${pkgver}/postgres_exporter-${pkgver}.linux-amd64.tar.gz"
  "${pkgname}.default"
  "${pkgname}.service"
  "${pkgname}.yml"
  "${pkgname}-consul.hcl"
  "set-${pkgname}.sh"
  "prometheus.sysusers"
  "${pkgname}.tmpfiles"
)
sha256sums=('cb89fc5bf4485fb554e0d640d9684fae143a4b2d5fa443009bd29c59f9129e84'
  'ec2e8b45fc1aaf0e61f2a8547e194d448ca2823b4d19cb6e23591750bdacbeae'
  '4f933f42565ad4234e43ace2a7cfee2ee3e38ed7c7310d5b9c062a84c4a66813'
  '01ba4719c80b6fe911b091a7c05124b64eeece964e09c058ef8f9805daca546b'
  'c4c0f71ab8d441a51ad0cfcdf0732ec10dbb1df6563f8cc8a43afd2a2300c46a'
  '7f9e5658369bb5524fef1129e52d02af6fb05a627ac59aa032d9ede3b42efd78'
  '48e22f1cf5592c90a62683f3e551fde213258b980d0858d8a7927da0273fe746'
  '94aabe32a027064141efcab5c7977a72feaf579486221e666cfb7fe2c2c537f2')

depends=(
  "systemd"
)

conflicts=(
  'prometheus-postgres-exporter'
)

backup=(
  "etc/default/${pkgname}"
)

package() {
  cd "${srcdir}/"

  install -Dm644 "${pkgname}.tmpfiles" \
  "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"

  cd "${srcdir}/postgres_exporter-${pkgver}.linux-amd64"

  install -Dm 755 "postgres_exporter" \
  "${pkgdir}/usr/bin/${pkgname}"

  install -Dm 755 "${srcdir}/set-${pkgname}.sh" \
  "${pkgdir}/usr/bin/set-${pkgname}.sh"

  install -Dm 644 "${srcdir}/${pkgname}.service" \
  "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -Dm 644 "${srcdir}/${pkgname}.default" \
  "${pkgdir}/etc/default/${pkgname}"
  install -Dm 644 "${srcdir}/${pkgname}.yml" \
  "${pkgdir}/etc/carbonio/${pkgname}/${pkgname}.yml"

  install -Dm 644 "${srcdir}/prometheus.sysusers" \
  "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  install -Dm 644 "${srcdir}/${pkgname}-consul.hcl" \
  "${pkgdir}/etc/zextras/service-discover/${pkgname}.hcl"
}

postinst() {
  if hash initctl 2>/dev/null; then
    initctl reload-configuration 2>/dev/null || true
  fi

  systemd-sysusers >/dev/null || true
  systemd-tmpfiles --create >/dev/null || true

  /usr/bin/set-carbonio-prometheus-postgres-exporter.sh

  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
    systemctl enable carbonio-prometheus-postgres-exporter.service >/dev/null 2>&1 || :
    systemctl start carbonio-prometheus-postgres-exporter.service >/dev/null 2>&1 || :
  fi

  echo "======================================================"
  echo "Carbonio Postgres Exporter installed successfully!"
  echo "You must reload service discover or add this host to prometheus.yml"
  echo "======================================================"
}

prerm() {
  if [ -d /run/systemd/system ]; then
    systemctl --no-reload disable carbonio-prometheus-postgres-exporter.service >/dev/null 2>&1 || :
    systemctl stop carbonio-prometheus-postgres-exporter.service >/dev/null 2>&1 || :
  fi
}

postrm() {
  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
  fi
}
