pkgname="carbonio-prometheus-process-exporter"
pkgver="0.8.3"
pkgrel="1"
pkgdesc="Process exporter for open-source systems monitoring and alerting toolkit"
maintainer="Zextras <packages@zextras.com>"
url="https://zextras.com"
section="mail"
priority="optional"
arch=('x86_64')
license=('Apache-2.0')
source=(
  "https://github.com/ncabatoff/process-exporter/releases/download/v${pkgver}/process-exporter-${pkgver}.linux-amd64.tar.gz"
  "${pkgname}.default"
  "${pkgname}.service"
  "${pkgname}.yml"
  "${pkgname}-consul.hcl"
  "prometheus.sysusers"
  "${pkgname}.tmpfiles"
)
sha256sums=('249db36771a4e66eaacca0ce31294de200df30eaf59a190c46639b98c5815969'
  '6212db4a4388ab4ecb32276edafa2cad62a836bcec790f3c5cb8c722448e50c1'
  '68cfaf3e6920698edfaa35916b3a81b163ee5db2ee68e4d2863484955ca2f10f'
  '1236102292804f5eb3ab73458944275e42bc82d793752565253583b72d885bfe'
  'aef359c9aa73a24ca4b83e94253d7c76ced2fd2ed7f0b4e661fe7b4323aa0b19'
  '48e22f1cf5592c90a62683f3e551fde213258b980d0858d8a7927da0273fe746'
  '3ba6e846d12c6df853ccfcc07737b028becff4a39dc53b250759372554397c88')

depends=(
  "systemd"
)

makedepends__apt=(
  "libsystemd-dev"
)

makedepends__yum=(
  "systemd-devel"
)

conflicts=(
  "prometheus-process-exporter"
)

backup=(
  "etc/default/${pkgname}"
  "etc/carbonio/${pkgname}/${pkgname}.yml"
)

package() {
  cd "${srcdir}/"

  install -Dm644 "${pkgname}.tmpfiles" \
    "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"

  cd "${srcdir}/process-exporter-${pkgver}.linux-amd64"

  install -Dm 755 process-exporter \
    "${pkgdir}/usr/bin/${pkgname}"

  install -Dm 644 "${srcdir}/${pkgname}.service" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -Dm 644 "${srcdir}/${pkgname}.default" \
    "${pkgdir}/etc/default/${pkgname}"
  install -Dm 644 "${srcdir}/${pkgname}.yml" \
    "${pkgdir}/etc/carbonio/${pkgname}/${pkgname}.yml"

  install -Dm 644 "${srcdir}"/prometheus.sysusers \
    "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  install -Dm 644 "${srcdir}"/${pkgname}-consul.hcl \
    "${pkgdir}/etc/zextras/service-discover/${pkgname}.hcl"

}

postinst() {
  if hash initctl 2>/dev/null; then
    initctl reload-configuration 2>/dev/null || true
  fi

  systemd-sysusers >/dev/null || true
  systemd-tmpfiles --create >/dev/null || true

  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
    systemctl enable carbonio-prometheus-process-exporter.service >/dev/null 2>&1 || :
    systemctl start carbonio-prometheus-process-exporter.service >/dev/null 2>&1 || :
  fi

  echo "======================================================"
  echo "Carbonio Process Exporter installed successfully!"
  echo "You must reload service discover or add this host to prometheus.yml"
  echo "======================================================"
}

prerm() {
  if [ -d /run/systemd/system ]; then
    systemctl --no-reload disable carbonio-prometheus-process-exporter.service >/dev/null 2>&1 || :
    systemctl stop carbonio-prometheus-process-exporter.service >/dev/null 2>&1 || :
  fi
}

postrm() {
  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
  fi
}
