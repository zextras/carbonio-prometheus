pkgname="carbonio-prometheus"
pkgver="2.48.1"
pkgrel="1"
pkgdesc="An open-source systems monitoring and alerting toolkit"
maintainer="Zextras <packages@zextras.com>"
url="https://zextras.com"
section="mail"
priority="optional"
arch=('x86_64')
license=('Apache-2.0')
source=(
  "https://github.com/prometheus/prometheus/releases/download/v${pkgver}/prometheus-${pkgver}.linux-amd64.tar.gz"
  "prometheus.default"
  "prometheus.service"
  "prometheus.sysusers"
  "prometheus.yml"
  "carbonio-prometheus.sh"
  "policies.json"
)
sha256sums=('40d6db7305b11e645bb43d7f3f4025c6394743c0b9e4f3029db2db80198e37d4'
  'cc41da9ca3507e01ca0ab953cdd009c84e19d1788835115ec6e2779678543a72'
  'b986e65191da8334d70b9f7545784bacae2425f90db29e124bb7e5f953f96acc'
  'b15c53ad5dd81f03e3af90e8a78a1a0e84e68af4de2beb18a9851bb4e8754224'
  "SKIP"
  'cdb7b2b2f5ac90c4f3958e584a4c5899c250db1b4775e34f1a242e5713443192'
  '9a3cb3da73ca92b641e9ac31223deadc0587c9ea280884a8ac8e80ab4266aa25')

depends=(
  "systemd"
)

conflicts=(
  "prometheus"
)

backup=(
  "etc/carbonio/${pkgname}/prometheus.yml"
)

package() {
  cd "${srcdir}/prometheus-${pkgver}.linux-amd64"

  install -Dm 755 prometheus "${pkgdir}/usr/bin/prometheus"
  install -Dm 755 promtool "${pkgdir}/usr/bin/promtool"

  install -dm 750 -o210 -g210 "${pkgdir}/var/lib/prometheus"

  install -Dm 640 -g210 "${srcdir}/prometheus.yml" \
    "${pkgdir}/etc/carbonio/${pkgname}/prometheus.yml"
  install -Dm 640 -g210 "${srcdir}/policies.json" \
    "${pkgdir}/etc/carbonio/${pkgname}/service-discover/policies.json"
  cp -r consoles* "${pkgdir}/etc/carbonio/${pkgname}/"

  install -Dm 755 "${srcdir}/carbonio-prometheus.sh" \
    "${pkgdir}/etc/zextras/pending-setups.d/carbonio-prometheus.sh"
  install -Dm 644 "${srcdir}/prometheus.service" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -Dm 644 "${srcdir}/prometheus.sysusers" \
    "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  install -Dm 644 "${srcdir}/prometheus.default" \
    "${pkgdir}/etc/default/${pkgname}"
}

postinst() {
  if hash initctl 2>/dev/null; then
    initctl reload-configuration 2>/dev/null || true
  fi

  systemd-sysusers >/dev/null || true
  systemd-tmpfiles --create >/dev/null || true

  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
    systemctl enable ${pkgname}.service >/dev/null 2>&1 || :
  fi

  echo "======================================================"
  echo "Carbonio Prometheus installed successfully!"
  echo "You must run pending-setups to configure it."
  echo "======================================================"
}

prerm() {
  if [ -d /run/systemd/system ]; then
    systemctl --no-reload disable ${pkgname} >/dev/null 2>&1 || :
    systemctl stop ${pkgname} >/dev/null 2>&1 || :
  fi
}

postrm() {
  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
  fi
}
