pkgname="carbonio-prometheus-mysqld-exporter"
pkgver="0.15.0"
pkgrel="1"
pkgdesc="Mysqld exporter for open-source systems monitoring and alerting toolkit"
maintainer="Zextras <packages@zextras.com>"
url="https://zextras.com"
section="mail"
priority="optional"
arch=('x86_64')
source=(
  "https://github.com/prometheus/mysqld_exporter/releases/download/v${pkgver}/mysqld_exporter-${pkgver}.linux-amd64.tar.gz"
  "${pkgname}.default"
  "${pkgname}.service"
  "${pkgname}-consul.hcl"
  "set-${pkgname}.sh"
  "prometheus.sysusers"
)

sha256sums=('3973db1c46b0323a957a43916b759ee71ddab9096958ce78401fdff894b0dc51'
  'd6c82a6c806a25a87d4bda6aa6a08af0af7a2528f4773dc6ee78e98f6d7b587e'
  'e2ab2353c1149f357f849781fc28b49d30726aef1c447ae4b43051160d2817a9'
  '29d76d86b207927994a773c82e9e93df4d69ddb54c9ed5142966b7a0d43da772'
  '1ddc0311e6391b262db7c62ac26a9f4949a6e0a082eb9d3f3bac8b9fb505a4f8'
  'b15c53ad5dd81f03e3af90e8a78a1a0e84e68af4de2beb18a9851bb4e8754224')

depends=(
  "systemd"
)

conflicts=(
  'prometheus-mysqld-exporter'
)

package() {
  cd "${srcdir}/mysqld_exporter-${pkgver}.linux-amd64"

  install -Dm 755 mysqld_exporter \
    "${pkgdir}/usr/bin/${pkgname}"

  install -Dm 755 "${srcdir}/set-${pkgname}.sh" \
    "${pkgdir}/usr/bin/set-${pkgname}.sh"

  install -Dm 644 "${srcdir}/${pkgname}.service" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"

  install -Dm 644 "${srcdir}/${pkgname}.default" \
    "${pkgdir}/etc/default/${pkgname}"

  install -Dm 644 "${srcdir}/prometheus.sysusers" \
    "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  install -Dm 644 "${srcdir}/${pkgname}-consul.hcl" \
    "${pkgdir}/etc/zextras/service-discover/${pkgname}.hcl"
}

postinst() {
  if hash initctl 2>/dev/null; then
    initctl reload-configuration 2>/dev/null || true
  fi

  systemd-sysusers >/dev/null || true
  systemd-tmpfiles --create >/dev/null || true

  /usr/bin/set-carbonio-prometheus-mysqld-exporter.sh

  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
    systemctl enable carbonio-prometheus-mysqld-exporter.service >/dev/null 2>&1 || :
    systemctl start carbonio-prometheus-mysqld-exporter.service >/dev/null 2>&1 || :
  fi

  echo "======================================================"
  echo "Carbonio Mysql Exporter installed successfully!"
  echo "You must reload service discover or add this host to prometheus.yml"
  echo "======================================================"
}

prerm() {
  if [ -d /run/systemd/system ]; then
    systemctl --no-reload disable carbonio-prometheus-mysqld-exporter.service >/dev/null 2>&1 || :
    systemctl stop carbonio-prometheus-mysqld-exporter.service >/dev/null 2>&1 || :
  fi
}

postrm() {
  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
  fi
}
