pkgname="carbonio-prometheus-mysqld-exporter"
pkgver="0.15.1"
pkgrel="2"
pkgdesc="Mysqld exporter for open-source systems monitoring and alerting toolkit"
maintainer="Zextras <packages@zextras.com>"
url="https://zextras.com"
section="mail"
priority="optional"
arch=('x86_64')
license=('Apache-2.0')
source=(
  "https://github.com/prometheus/mysqld_exporter/releases/download/v${pkgver}/mysqld_exporter-${pkgver}.linux-amd64.tar.gz"
  "${pkgname}.default"
  "${pkgname}.service"
  "${pkgname}-consul.hcl"
  "set-${pkgname}.sh"
  "prometheus.sysusers"
  "${pkgname}.tmpfiles"
  "${pkgname}.cnf"
  "${pkgname}-setup"
)

sha256sums=('85ea5bc68e1b9f466c1df10ff016652dd210371d42245e012b876265e89ae29d'
  'd6c82a6c806a25a87d4bda6aa6a08af0af7a2528f4773dc6ee78e98f6d7b587e'
  '86e9412e5ae82c9b49dac2892b9656223d958f456e1de84e437ee8a7fab920c0'
  '8f7f35a1ca4fdbfea351f9dad62b5f50ec059b7e18314c9137ab655eca336064'
  '0fbd4dd75af78a251168a21d52c237eaa93e392469b8e54d2b0a53e0c6a9975e'
  '48e22f1cf5592c90a62683f3e551fde213258b980d0858d8a7927da0273fe746'
  '6f76239ebd4ecc86758e73f028960249fe343e407d1b1cf38f11b9e1c22047df'
  'e3b0c44298fc1c149afbf4c8996fb92427ae41e4649b934ca495991b7852b855'
  'bdc1408a30296dbb47682f65be31d8ec4419afe6139241a44dc1e0612df19a46')

depends=(
  "systemd"
)

conflicts=(
  'prometheus-mysqld-exporter'
)

package() {
  cd "${srcdir}/"

  install -Dm644 "${pkgname}.tmpfiles" \
    "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"

  cd "${srcdir}/mysqld_exporter-${pkgver}.linux-amd64"

  install -Dm 755 mysqld_exporter \
    "${pkgdir}/usr/bin/${pkgname}"

  install -Dm 755  "${srcdir}/${pkgname}-setup"\
    "${pkgdir}/usr/bin/${pkgname}-setup"

  install -Dm 755 "${srcdir}/set-${pkgname}.sh" \
    "${pkgdir}/etc/zextras/pending-setups.d/set-${pkgname}.sh"

  install -Dm 644 "${srcdir}/${pkgname}.service" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -Dm 644 "${srcdir}/${pkgname}.default" \
    "${pkgdir}/etc/default/${pkgname}"
  install -Dm 755 "${srcdir}/${pkgname}.cnf" \
    "${pkgdir}/etc/carbonio/${pkgname}/${pkgname}.cnf"

  install -Dm 644 "${srcdir}/prometheus.sysusers" \
    "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  install -Dm 644 "${srcdir}/${pkgname}-consul.hcl" \
    "${pkgdir}/etc/zextras/service-discover/${pkgname}.hcl"
}

postinst() {
  if hash initctl 2>/dev/null; then
    initctl reload-configuration 2>/dev/null || true
  fi

  systemd-sysusers >/dev/null || true
  systemd-tmpfiles --create >/dev/null || true

  #/usr/bin/set-carbonio-prometheus-mysqld-exporter.sh

  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
    systemctl enable carbonio-prometheus-mysqld-exporter.service >/dev/null 2>&1 || :
    systemctl start carbonio-prometheus-mysqld-exporter.service >/dev/null 2>&1 || :
  fi

  echo "======================================================"
  echo "Carbonio Mysql Exporter installed successfully!"
  echo "You must run pending-setups to configure it and reload service discover or add this host to prometheus.yml"
  echo "======================================================"
}

prerm() {
  if [ -d /run/systemd/system ]; then
    systemctl --no-reload disable carbonio-prometheus-mysqld-exporter.service >/dev/null 2>&1 || :
    systemctl stop carbonio-prometheus-mysqld-exporter.service >/dev/null 2>&1 || :
  fi

  rm -f /etc/zextras/pending-setups.d/set-carbonio-prometheus-mysqld-exporter.sh
  rm -f /etc/zextras/pending-setups.d/done/set-carbonio-prometheus-mysqld-exporter.sh
}

postrm() {
  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
  fi
}
