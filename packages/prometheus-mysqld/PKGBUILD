pkgname="carbonio-prometheus-mysqld-exporter"
pkgver="0.15.1"
pkgrel="3"
pkgdesc="Mysqld exporter for open-source systems monitoring and alerting toolkit"
maintainer="Zextras <packages@zextras.com>"
url="https://zextras.com"
section="mail"
priority="optional"
arch=('x86_64')
license=('Apache-2.0')
source=(
  "https://github.com/prometheus/mysqld_exporter/releases/download/v${pkgver}/mysqld_exporter-${pkgver}.linux-amd64.tar.gz"
  "${pkgname}.default"
  "${pkgname}.service"
  "${pkgname}-consul.hcl"
  "set-${pkgname}.sh"
  "prometheus.sysusers"
  "${pkgname}.tmpfiles"
  "${pkgname}.cnf"
)

sha256sums=('85ea5bc68e1b9f466c1df10ff016652dd210371d42245e012b876265e89ae29d'
  'd6c82a6c806a25a87d4bda6aa6a08af0af7a2528f4773dc6ee78e98f6d7b587e'
  'e2ab2353c1149f357f849781fc28b49d30726aef1c447ae4b43051160d2817a9'
  '8f7f35a1ca4fdbfea351f9dad62b5f50ec059b7e18314c9137ab655eca336064'
  '14c6cf3017a3b31fff77215f79e89d1c83bf994f2e78fa97d9b8e4ccdf613f29'
  '48e22f1cf5592c90a62683f3e551fde213258b980d0858d8a7927da0273fe746'
  '6f76239ebd4ecc86758e73f028960249fe343e407d1b1cf38f11b9e1c22047df'
  '334736e422cbcc875f1c6d87c5e2f885ea3daa4ee4c981205314e64581c2e68e')

depends=(
  "systemd"
)

conflicts=(
  'prometheus-mysqld-exporter'
)

package() {
  cd "${srcdir}/"

  install -Dm644 "${pkgname}.tmpfiles" \
  "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"

  cd "${srcdir}/mysqld_exporter-${pkgver}.linux-amd64"

  install -Dm 755 mysqld_exporter \
  "${pkgdir}/usr/bin/${pkgname}"

  install -Dm 755 "${srcdir}/set-${pkgname}.sh" \
  "${pkgdir}/etc/zextras/pending-setups.d/set-${pkgname}.sh"

  install -Dm 644 "${srcdir}/${pkgname}.service" \
  "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -Dm 644 "${srcdir}/${pkgname}.default" \
  "${pkgdir}/etc/default/${pkgname}"
  install -Dm 644 "${srcdir}/${pkgname}.cnf" \
  "${pkgdir}/etc/carbonio/${pkgname}/${pkgname}.cnf"

  install -Dm 644 "${srcdir}/prometheus.sysusers" \
  "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  install -Dm 644 "${srcdir}/${pkgname}-consul.hcl" \
  "${pkgdir}/etc/zextras/service-discover/${pkgname}.hcl"
}

postinst() {
  if hash initctl 2>/dev/null; then
    initctl reload-configuration 2>/dev/null || true
  fi

  systemd-sysusers >/dev/null || true
  systemd-tmpfiles --create >/dev/null || true

  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
    systemctl enable carbonio-prometheus-mysqld-exporter.service >/dev/null 2>&1 || :
    systemctl start carbonio-prometheus-mysqld-exporter.service >/dev/null 2>&1 || :
  fi

  echo "======================================================"
  echo "Carbonio Mysql Exporter installed successfully!"
  echo "You must run pending-setups to configure it correctly."
  echo "======================================================"
}

prerm() {
  if [ -d /run/systemd/system ]; then
    systemctl --no-reload disable carbonio-prometheus-mysqld-exporter.service >/dev/null 2>&1 || :
    systemctl stop carbonio-prometheus-mysqld-exporter.service >/dev/null 2>&1 || :
  fi

  rm -f /etc/zextras/pending-setups.d/set-carbonio-prometheus-mysqld-exporter.sh
  rm -f /etc/zextras/pending-setups.d/done/set-carbonio-prometheus-mysqld-exporter.sh
}

postrm() {
  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
  fi
}
