pkgname="carbonio-prometheus-nginx-exporter"
pkgver="0.11.0"
pkgrel="2"
pkgdesc="Nginx exporter for open-source systems monitoring and alerting toolkit"
maintainer="Zextras <packages@zextras.com>"
url="https://zextras.com"
section="mail"
priority="optional"
arch=('x86_64')
license=('Apache-2.0')
source=(
  "https://github.com/nginxinc/nginx-prometheus-exporter/releases/download/v${pkgver}/nginx-prometheus-exporter_${pkgver}_linux_amd64.tar.gz"
  "${pkgname}.default"
  "${pkgname}.service"
  "${pkgname}-consul.hcl"
  "prometheus.sysusers"
)
sha256sums=(
  '2b8b693ce006406db200c7bd9987453b7055e4e98511fd7b36d492fc68bfc0f4'
  'cf527b2582f6347c5e6591458d43e3506914d0a46cd527bdc2117c13699c3bd4'
  'ca7a63fe141c170d67e3c8f8af33d21775a395379671d171b91b183c9374f8e4'
  '05f5256d05feca550fddddc4c66744b3d1dba3568d05f9e5bfcab64b4aa1a351'
  '4ac56c918df18fff8a6f87801e4ddb25bdd6c61e982fc06f06774ae4b1364128')

depends=(
  "systemd"
)

conflicts=(
  "prometheus-nginx-exporter"
)

backup=(
  "etc/zextras/service-discover/${pkgname}.hcl"
)

package() {
  install -Dm 755 "${srcdir}/nginx-prometheus-exporter" \
  "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 "${srcdir}/${pkgname}.service" \
  "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -Dm 644 "${srcdir}/${pkgname}.default" \
  "${pkgdir}/etc/default/${pkgname}"

  install -Dm 644 "${srcdir}/prometheus.sysusers" \
  "${pkgdir}/usr/lib/sysusers.d/${pkgname}".conf
  install -Dm 644 "${srcdir}/${pkgname}-consul.hcl" \
  "${pkgdir}//etc/zextras/service-discover/${pkgname}.hcl"
}

postinst() {
  # required until zextras user creation will be made with sysusers.d
  getent group 'zextras' >/dev/null ||
    groupadd -r 'zextras'
  getent passwd 'zextras' >/dev/null ||
    useradd -r -M -d '/opt/zextras' -g 'zextras' -s /bin/bash 'zextras'

  if hash initctl 2>/dev/null; then
    initctl reload-configuration 2>/dev/null || true
  fi

  systemd-sysusers >/dev/null || true
  systemd-tmpfiles --create >/dev/null || true

  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
    systemctl enable carbonio-prometheus-nginx-exporter.service >/dev/null 2>&1 || :
    systemctl start carbonio-prometheus-nginx-exporter.service >/dev/null 2>&1 || :
  fi

  echo "======================================================"
  echo "Carbonio Nginx Exporter installed successfully!"
  echo "You must reload service discover or add this host to prometheus.yml"
  echo "======================================================"
}

prerm() {
  if [ -d /run/systemd/system ]; then
    systemctl --no-reload disable carbonio-prometheus-nginx-exporter.service >/dev/null 2>&1 || :
    systemctl stop carbonio-prometheus-nginx-exporter.service >/dev/null 2>&1 || :
  fi
}

postrm() {
  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
  fi
}
