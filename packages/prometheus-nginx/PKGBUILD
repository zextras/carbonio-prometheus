pkgname="carbonio-prometheus-nginx-exporter"
pkgver="1.4.2"
pkgrel="1"
pkgdesc="Nginx exporter for open-source systems monitoring and alerting toolkit"
maintainer="Zextras <packages@zextras.com>"
url="https://zextras.com"
section="mail"
priority="optional"
arch=('x86_64')
license=('Apache-2.0')
source=(
  "https://github.com/nginxinc/nginx-prometheus-exporter/releases/download/v${pkgver}/nginx-prometheus-exporter_${pkgver}_linux_amd64.tar.gz"
  "${pkgname}.default"
  "${pkgname}.service"
  "${pkgname}-consul.hcl"
  "prometheus.sysusers"
)
sha256sums=('382125866a1dc4ae6ac5355bb462040a7f0d96d9d1bd59658c2d5f80b49e3da6'
  '6927553baccafe09cb9ba0a9117020c9099b2edc68865b5e8a7b78797fafcf08'
  '4ac56c918df18fff8a6f87801e4ddb25bdd6c61e982fc06f06774ae4b1364128'
  'be8e25a6cc4dba290c6039b7c27f0e22e030c4a70320b8f71dde03492dca538e'
  '48e22f1cf5592c90a62683f3e551fde213258b980d0858d8a7927da0273fe746')

depends=(
  "systemd"
)

conflicts=(
  "prometheus-nginx-exporter"
)

backup=(
  "etc/zextras/service-discover/${pkgname}.hcl"
)

package() {
  install -Dm 755 "${srcdir}/nginx-prometheus-exporter" \
    "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 "${srcdir}/${pkgname}.service" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -Dm 644 "${srcdir}/${pkgname}.default" \
    "${pkgdir}/etc/default/${pkgname}"

  install -Dm 644 "${srcdir}/prometheus.sysusers" \
    "${pkgdir}/usr/lib/sysusers.d/${pkgname}".conf
  install -Dm 644 "${srcdir}/${pkgname}-consul.hcl" \
    "${pkgdir}/etc/zextras/service-discover/${pkgname}.hcl"
}

postinst() {
  # required until zextras user creation will be made with sysusers.d
  getent group 'zextras' >/dev/null ||
    groupadd -r 'zextras'
  getent passwd 'zextras' >/dev/null ||
    useradd -r -M -d '/opt/zextras' -g 'zextras' -s /bin/bash 'zextras'

  if hash initctl 2>/dev/null; then
    initctl reload-configuration 2>/dev/null || true
  fi

  systemd-sysusers >/dev/null || true
  systemd-tmpfiles --create >/dev/null || true

  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
    systemctl enable carbonio-prometheus-nginx-exporter.service >/dev/null 2>&1 || :
    systemctl start carbonio-prometheus-nginx-exporter.service >/dev/null 2>&1 || :
  fi

  echo "======================================================"
  echo "Carbonio Nginx Exporter installed successfully!"
  echo "You must reload service discover or add this host to prometheus.yml"
  echo "======================================================"
}

prerm() {
  if [ -d /run/systemd/system ]; then
    systemctl --no-reload disable carbonio-prometheus-nginx-exporter.service >/dev/null 2>&1 || :
    systemctl stop carbonio-prometheus-nginx-exporter.service >/dev/null 2>&1 || :
  fi
}

postrm() {
  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
  fi
}
