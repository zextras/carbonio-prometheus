# _gitname="clamav-prometheus-exporter-master"
pkgname="carbonio-prometheus-clamav-exporter"
pkgdesc="Prometheus-Exporter for ClamAV"
pkgver="0.1.0"
maintainer="Zextras <packages@zextras.com>"
pkgrel="1"
section="mail"
priority="optional"
arch=('x86_64')
url="https://zextras.com"
license=("Apache-2.0")
source=(
        "https://github.com/r3kzi/clamav-prometheus-exporter/archive/refs/heads/master.zip"
        "${pkgname}.service"
        "${pkgname}-consul.hcl"
        "prometheus.sysusers"
        "${pkgname}.default"
)

depends=(
  "systemd"
)

makedepends=(
  "git"
)

sha256sums=(
  "SKIP"
  "SKIP"
  "SKIP"
  "SKIP"
  "SKIP"
)

 build(){
  cd "${srcdir}/clamav-prometheus-exporter-master/"

    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly"

    go build -o "${srcdir}/clamav-prometheus-exporter-master/carbonio-prometheus-clamav-exporter"
 }

package() {
  cd "${srcdir}/"

  install -Dm 755 "./clamav-prometheus-exporter-master/carbonio-prometheus-clamav-exporter" \
    "${pkgdir}/usr/bin/${pkgname}"

  install -Dm 644 "${pkgname}.service" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -Dm 644 "${pkgname}.default" \
    "${pkgdir}/etc/default/${pkgname}"

#  install -Dm 644 -g210 "${srcdir}/${pkgname}.yml" \
#    "${pkgdir}/etc/carbonio/${pkgname}/${pkgname}.yml"
  install -Dm 644 "prometheus.sysusers" \
    "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  install -Dm 644 "${pkgname}-consul.hcl" \
    "${pkgdir}/etc/zextras/service-discover/${pkgname}.hcl"

}

postinst() {
  if hash initctl 2>/dev/null; then
    initctl reload-configuration 2>/dev/null || true
  fi

  systemd-sysusers >/dev/null || true
  systemd-tmpfiles --create >/dev/null || true

  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
    systemctl enable carbonio-prometheus-clamav-exporter.service >/dev/null 2>&1 || :
    systemctl start carbonio-prometheus-clamav-exporter.service >/dev/null 2>&1 || :
  fi

  echo "======================================================"
  echo "Carbonio ClamAV Exporter installed successfully!"
  echo "You must reload service discover or add this host to prometheus.yml"
  echo "======================================================"
}

prerm() {
  if [ -d /run/systemd/system ]; then
    systemctl --no-reload disable carbonio-prometheus-clamav-exporter.service >/dev/null 2>&1 || :
    systemctl stop carbonio-prometheus-clamav-exporter.service >/dev/null 2>&1 || :
  fi
}

postrm() {
  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
  fi
}