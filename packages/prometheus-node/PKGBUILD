pkgname="carbonio-prometheus-node-exporter"
pkgver="1.6.1"
pkgrel="1"
pkgdesc="Node exporter for open-source systems monitoring and alerting toolkit"
maintainer="Zextras <packages@zextras.com>"
url="https://zextras.com"
section="mail"
priority="optional"
arch=('x86_64')
license=('Apache-2.0')
source=(
  "https://github.com/prometheus/node_exporter/releases/download/v${pkgver}/node_exporter-${pkgver}.linux-amd64.tar.gz"
  "${pkgname}.default"
  "${pkgname}-consul.hcl"
  "${pkgname}.service"
  "prometheus.sysusers"
)
sha256sums=(
  'ecc41b3b4d53f7b9c16a370419a25a133e48c09dfc49499d63bcc0c5e0cf3d01'
  '8f7ca757c4a98ce33e77c41c1915c6699173dc5535af3c6006b6e091ef7f9d11'
  'dad0dc366ca725978de9ff45522ac51e1b414dbcbd4f3be9ac891d2e19ca8671'
  '20672ccb0e218f17d29a6ac3ac7a6af2dccbba002d9a66cbf8c49da239c3df4d'
  'b15c53ad5dd81f03e3af90e8a78a1a0e84e68af4de2beb18a9851bb4e8754224')

depends=(
  "systemd"
)

backup=(
  "etc/default/${pkgname}"
)

conflicts=(
  "prometheus-node-exporter"
)

package() {
  cd "${srcdir}/node_exporter-${pkgver}.linux-amd64"

  install -Dm 755 node_exporter \
  "${pkgdir}/usr/bin/${pkgname}"

  install -Dm 644 "${srcdir}/${pkgname}.service" \
  "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -Dm 644 "${srcdir}/${pkgname}.default" \
  "${pkgdir}/etc/default/${pkgname}"

  install -Dm 644 "${srcdir}/prometheus.sysusers" \
  "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  install -Dm 644 "${srcdir}/${pkgname}-consul.hcl" \
  "${pkgdir}/etc/zextras/service-discover/${pkgname}.hcl"
}

postinst() {
  if hash initctl 2>/dev/null; then
    initctl reload-configuration 2>/dev/null || true
  fi

  systemd-sysusers >/dev/null || true
  systemd-tmpfiles --create >/dev/null || true

  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
    systemctl enable carbonio-prometheus-node-exporter.service >/dev/null 2>&1 || :
    systemctl start carbonio-prometheus-node-exporter.service >/dev/null 2>&1 || :
  fi

  echo "======================================================"
  echo "Carbonio Node Exporter installed successfully!"
  echo "You must reload service discover or add this host to prometheus.yml"
  echo "======================================================"
}

prerm() {
  if [ -d /run/systemd/system ]; then
    systemctl --no-reload disable carbonio-prometheus-node-exporter.service >/dev/null 2>&1 || :
    systemctl stop carbonio-prometheus-node-exporter.service >/dev/null 2>&1 || :
  fi
}

postrm() {
  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
  fi
}
