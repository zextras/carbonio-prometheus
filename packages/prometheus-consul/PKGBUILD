pkgname="carbonio-prometheus-consul-exporter"
pkgver="0.9.0"
pkgrel="1"
pkgdesc="Consul exporter for open-source systems monitoring and alerting toolkit"
maintainer="Zextras <packages@zextras.com>"
url="https://zextras.com"
section="mail"
priority="optional"
arch=('x86_64')
license=('Apache-2.0')
source=(
  "https://github.com/prometheus/consul_exporter/releases/download/v${pkgver}/consul_exporter-${pkgver}.linux-amd64.tar.gz"
  "${pkgname}.default"
  "${pkgname}.service"
  "${pkgname}-consul.hcl"
  "prometheus.sysusers"
)
sha256sums=(
  '918bbbfaec650a9a1159edc663c7b09d6477cd96c078edef6d9fe608ece3c4ff'
  '0fc91310e2c3140bd90b2fad62f497d8e7432cf3a2a8ab3a034b45958e01012a'
  'fcf540494439a6a88387c198f283afa3e5718f253f499fedb9c42065f01e18cc'
  '76bd2d91f9dd666e3ec4e1d1df63ecc9d0e2f71d6f2646f3ba5e5e6bcb517cf3'
  '48e22f1cf5592c90a62683f3e551fde213258b980d0858d8a7927da0273fe746')

depends=(
  "systemd"
)

conflicts=(
  'prometheus-consul-exporter'
)

package() {

  cd "${srcdir}/consul_exporter-${pkgver}.linux-amd64"

  install -Dm 755 consul_exporter \
  "${pkgdir}/usr/bin/${pkgname}"

  install -Dm 644 "${srcdir}/${pkgname}.service" \
  "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -Dm 644 "${srcdir}/${pkgname}.default" \
  "${pkgdir}/etc/default/${pkgname}"

  install -Dm 644 "${srcdir}/prometheus.sysusers" \
  "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  install -Dm 644 "${srcdir}/${pkgname}-consul.hcl" \
  "${pkgdir}/etc/zextras/service-discover/${pkgname}.hcl"
}

postinst() {
  if hash initctl 2>/dev/null; then
    initctl reload-configuration 2>/dev/null || true
  fi

  systemd-sysusers >/dev/null || true
  systemd-tmpfiles --create >/dev/null || true

  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
    systemctl enable carbonio-prometheus-consul-exporter.service >/dev/null 2>&1 || :
    systemctl start carbonio-prometheus-consul-exporter.service >/dev/null 2>&1 || :
  fi

  echo "======================================================"
  echo "Carbonio Consul Exporter installed successfully!"
  echo "You must reload service discover or add this host to prometheus.yml"
  echo "======================================================"
}

prerm() {
  if [ -d /run/systemd/system ]; then
    systemctl --no-reload disable carbonio-prometheus-consul-exporter.service >/dev/null 2>&1 || :
    systemctl stop carbonio-prometheus-consul-exporter.service >/dev/null 2>&1 || :
  fi
}

postrm() {
  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
  fi
}
