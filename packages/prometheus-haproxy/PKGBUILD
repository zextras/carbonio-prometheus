pkgname="carbonio-prometheus-haproxy-exporter"
pkgver="0.15.0"
pkgrel="1"
pkgdesc="Prometheus exporter for haproxy"
maintainer="Zextras <packages@zextras.com>"
url="https://zextras.com"
section="mail"
priority="optional"
arch=('x86_64')
license=('Apache-2.0')
source=(
  "https://github.com/prometheus/haproxy_exporter/releases/download/v${pkgver}/haproxy_exporter-${pkgver}.linux-amd64.tar.gz"
  "${pkgname}.default"
  "${pkgname}.service"
  "${pkgname}-consul.hcl"
  "prometheus.sysusers"
)
sha256sums=('ac200872b734e2f9c0211997f7f9c0ca5ad6522996c37aed39a732d5f3c0de16'
  '514ef813f696c8c1c4c590a251a0e77aa0cd3f5c9d84f7b2ec837c9f5233e5eb'
  '84035f16c3d536ae4c74b13ca7ecb1de757c23e95c02dde7a3be2773fb43bdfb'
  '35436228b446abaefef2d9d184f5ecf4a7f9ca024c0744b59b4002dfe2b918b8'
  '48e22f1cf5592c90a62683f3e551fde213258b980d0858d8a7927da0273fe746')

depends=(
  "systemd"
)

conflicts=(
  "prometheus-haproxy-exporter"
)

backup=(
  "etc/zextras/service-discover/${pkgname}.hcl"
)

package() {
  install -Dm 755 "haproxy_exporter" \
    "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 "${srcdir}/${pkgname}.service" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -Dm 644 "${srcdir}/${pkgname}.default" \
    "${pkgdir}/etc/default/${pkgname}"

  install -Dm 644 "${srcdir}/prometheus.sysusers" \
    "${pkgdir}/usr/lib/sysusers.d/${pkgname}".conf
  install -Dm 644 "${srcdir}/${pkgname}-consul.hcl" \
    "${pkgdir}/etc/zextras/service-discover/${pkgname}.hcl"
}

postinst() {
  # required until zextras user creation will be made with sysusers.d
  getent group 'zextras' >/dev/null ||
    groupadd -r 'zextras'
  getent passwd 'zextras' >/dev/null ||
    useradd -r -M -d '/opt/zextras' -g 'zextras' -s /bin/bash 'zextras'

  if hash initctl 2>/dev/null; then
    initctl reload-configuration 2>/dev/null || true
  fi

  systemd-sysusers >/dev/null || true
  systemd-tmpfiles --create >/dev/null || true

  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
    systemctl enable carbonio-prometheus-haproxy-exporter.service >/dev/null 2>&1 || :
    systemctl start carbonio-prometheus-haproxy-exporter.service >/dev/null 2>&1 || :
  fi

  echo "======================================================"
  echo "Carbonio Haproxy Exporter installed successfully!"
  echo "You must reload service discover or add this host to prometheus.yml"
  echo "======================================================"
}

prerm() {
  if [ -d /run/systemd/system ]; then
    systemctl --no-reload disable carbonio-prometheus-haproxy-exporter.service >/dev/null 2>&1 || :
    systemctl stop carbonio-prometheus-haproxy-exporter.service >/dev/null 2>&1 || :
  fi
}

postrm() {
  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
  fi
}
